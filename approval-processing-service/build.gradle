plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.8'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'com.google.protobuf' version '0.9.4'
	id 'idea'
}

group = 'com.erp'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// gRPC & Protobuf 의존성
	implementation 'net.devh:grpc-spring-boot-starter:3.1.0.RELEASE'
	implementation 'io.grpc:grpc-netty-shaded:1.63.0'
	implementation 'io.grpc:grpc-protobuf:1.63.0'
	implementation 'io.grpc:grpc-stub:1.63.0'
	compileOnly 'org.apache.tomcat:annotations-api:6.0.53'

	implementation 'org.springframework.kafka:spring-kafka'
}

protobuf {
	protoc {
		artifact = "com.google.protobuf:protoc:3.25.1"
	}
	plugins {
		grpc {
			artifact = "io.grpc:protoc-gen-grpc-java:1.63.0"
		}
	}
	generateProtoTasks {
		all()*.plugins {
			grpc {}
		}
	}
}

// IntelliJ 소스 인식 설정
idea {
	module {
		sourceDirs += file("src/main/java")
		sourceDirs += file("build/generated/source/proto/main/java")
		sourceDirs += file("build/generated/source/proto/main/grpc")
		generatedSourceDirs += file("build/generated/source/proto/main/java")
		generatedSourceDirs += file("build/generated/source/proto/main/grpc")
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
